var blade_cd = item.GetCooldown(＂blade＂)
?item.GetCooldown(＂blade＂) > 999
  blade_cd = "999+"
:
  blade_cd = item.GetCooldown(＂blade＂)

>c-45,-7,== LVL TIMES ==
>c-45,-6,BST: @loc.bestTime@
>c-45,-5,AVG: @loc.averageTime@
>c-45,-4,CUR: @totaltime@
>c-45,-2,== ENEMY INFO ==
>c-45,-1,Name: @foe.name@
>c-45,0,Dist: @foe.distance@
^,@foe.GetCount(15)@
>c-45,1,S/T:  @foe.state@,@foe.time@
>c-45,3,== ITEM COOLDOWNS ==
>c-45,4,BFG: @blade_cd@
>c-35,4,BAR: @item.GetCooldown(＂bardiche＂)@
>c-45,5,HAM: @item.GetCooldown(＂hammer＂)@
>c-35,5,STF: @item.GetCooldown(＂quarterstaff＂)@
>c-45,6,BSH: @item.GetCooldown(＂bash＂)@
>c-35,6,DSH: @item.GetCooldown(＂dash＂)@
?item.GetCooldown(＂blade＂) <= 0 
  >c-40,4,#32CD32,RDY
?item.GetCooldown(＂bardiche＂) <= 0 
  >c-30,4,#32CD32,RDY
?item.GetCooldown(＂hammer＂) <= 0 
  >c-40,5,#32CD32,RDY
?item.GetCooldown(＂quarterstaff＂) <= 0 
  >c-30,5,#32CD32,RDY
?item.GetCooldown(＂bash＂) <= 0 
  >c-40,6,#32CD32,RDY
?item.GetCooldown(＂dash＂) <= 0 
  >c-30,6,#32CD32,RDY
>c-45,10,BUFFS:   @buffs.string@
>c-45,11,DEBUFFS: @debuffs.string@

//============================================//

func bar_on_cd()
  ?item.GetCooldown(＂bardiche＂) > 0
  ^& item.GetCooldown(＂bardiche＂) < 870
    return true

func ham_on_cd()
  ?item.GetCooldown(＂hammer＂) > 0
  ^& item.GetCooldown(＂hammer＂) < 645
    return true

func lootwalk()
  ?pickup.distance < 15
    equipL triskelion
    equipR star
  ?foe.distance > 23 & armor > 24
    equipL triskelion
    equipR quest

func bardiche_use()
  ?foe.distance <= 10 & item.CanActivate()
  ^& item.GetCooldown(＂bardiche＂) <= 0 
  ^| item.GetCooldown(＂bardiche＂) > 870
    equip bardiche
    activate R

func hammer_use()
  ?foe.distance <= 7 & item.CanActivate()
  ^& item.GetCooldown(＂hammer＂) <= 0
  ^| item.GetCooldown(＂hammer＂) > 645
  ^& item.GetCooldown(＂bardiche＂) > 0
    equip heavy hammer
    activate R

func mask_use()
  ?item.GetCooldown(＂mask＂) <= 0
  ^& item.CanActivate()
  ^& foe.distance < 25
    equip mask
    activate R
 
func blade_use()
  ?foe.GetCount(40) > 5 & foe ! boss &
  ^item.GetCooldown(＂blade＂) <= 0
    equip blade
    activate R
 
func bdashstaff_use()
  ?foe.distance <= 16 & foe.distance >= 11 & 
  ^item.GetCooldown(＂bash＂) <= 0
  ^& summon.count = 1 & foe ! yeti
    equipR bashing shield
  ?foe.distance <= 16 & foe.distance >= 11 & 
  ^item.GetCooldown(＂dash＂) <= 0
  ^& summon.count = 1 & foe ! yeti
    equipR dashing shield
  ?item.GetCooldown(＂quarterstaff＂) <= 0
  ^& item.CanActivate() & foe.distance > 6
  ^& foe ! boss & summon.count = 1
    equip quarterstaff
    activate R

func aether_summ()
  ?summon.count = 0 & item.CanActivate()
  ^& item.GetCooldown(＂aether_talisman＂) <= 0
    equipL triskelion
    equipR aether talisman
    activate R

func fire_summ()
  ?summon.count = 0 & item.CanActivate()
  ^& item.GetCooldown(＂fire_talisman＂) <= 0
    equipL triskelion
    equipR fire talisman
    activate R

func armor_regen()
  ?armor < 14 & foe.distance > 23
  ^& summon.count = 1
    equipL triskelion
    equipR compound shield +12
  ?armor <= 24 & armor >= 14
  ^& foe.distance > 23
  ^& summon.count = 1
    equipL triskelion
    equipR compound shield +11
  ?pickup.distance < 15
    equipL star
    equipR compound shield +12

//============================================//

func isEquippedTwoHanded()
  return !item.left
func AAC()
  ?!ai.enabled
    return
  var isTwoHanded = isEquippedTwoHanded()
  var leftItem = item.left
  var rightItem = item.right 
  ?item.right.state = 3
    equipR shield *0 +0
  ?item.left.state = 3
    equipL wand *0 +0
  ?isTwoHanded
    ?rightItem
      equip @rightItem@
    :?leftItem
      equip @leftItem@
  :
    ?rightItem
      equipR @rightItem@
    ?leftItem
      equipL @leftItem@

//============================================//

AAC()

?loc = rocky
  fire_summ()
:
  aether_summ()

?loc = ridge & summon.count = 1
  ?foe = yeti
    ?foe.armor > 0
      bardiche_use()
    ?foe.state = 132
      activate potion
    :?bar_on_cd()
      equip fire crossbow
  :?foe.GetCount(15) >= 3
    equipL ice wand
    equipR fire shield
  :?foe = ice pillar
    equipL fire sword +10
    equipR fire sword +9
  :?foe = ice wall
    equip fire crossbow
  :?foe = giant
    ?hp < maxhp
      equipL vigor sword +10
      equipR vigor sword +9
    :
      equipL fire sword +10
      equipR fire sword +9
  :
    equipL fire sword +10
    equipR fire shield

?loc = mine & summon.count = 1
  ?foe = guardian
    ?foe.state = 2
      activate potion
      equip aether crossbow
    :?foe.state = 33
      ?foe.distance <= 7
        bardiche_use()
      ?foe.armor > 1 & foe.distance <= 6
      ^& bar_on_cd()
        hammer_use()
      ?foe.distance < 19 & foe.distance > 10
      ^& foe.armor > 1
        equipL aether hammer +10
        equipR aether hammer +9
      ?foe.distance < 10 & foe.armor > 1
      ^& bar_on_cd() & ham_on_cd()
        equipL aether hammer +10
        equipR aether hammer +9
      ?foe.distance < 10 & foe.armor < 1
      ^& bar_on_cd() & ham_on_cd()
        equipL aether sword +10
        equipR aether sword +9
      ?foe.time = 210
        equip aether crossbow
    :?foe.state = 32 & foe.time = 25
      equipL mind
    :
      equip aether crossbow
  :?foe.state = 32 & foe.time = 200
    equip aether crossbow
  :?foe.distance < 17
  ^& foe.GetCount(15) >= 3
    equipL ice wand +10
    equipR aether shield
  :?foe = bomb & foe.distance < 5
    equipL mind
  :
    equipL aether sword +10
    equipR aether shield

?loc = halls & summon.count = 1
  ?foe.GetCount(15) >= 3 | foe = ghost
  ^& foe.distance <= 15
    equipL ice wand +10
    equipR ice wand +9
  :?foe = pallas
    ?foe.state = 2 & foe = legless
      activate potion
    ?foe.distance <= 7
      bardiche_use()
    ?bar_on_cd()
      equipL vigor sword +10
      equipR vigor sword +9
    ?foe.GetCount(15) >= 3 | foe = ghost
    ^& foe.distance <= 15
      equipL ice wand +10
      equipR ice wand +9
  :
    equipL vigor sword +10
    equipR vigor sword +9

?loc = forest & summon.count = 1
  ?foe.distance <= 15
    ?foe = puff & foe.distance < 5
      equipL mind
    :?foe = boss
      bardiche_use()
      ?bar_on_cd() & foe.distance <= 10
        equipL vigor sword +10
        equipR poison sword +10
    :?foe.GetCount(15) >= 3
      equipL ice wand +10
      equipR ice wand +9
    :
      equipL vigor sword +10
      equipR poison sword +10

?loc = caves & summon.count = 1
  ?foe = boss
    bardiche_use()
    ?bar_on_cd() & foe.distance <= 10
      equipL ice sword +10
      equipR vigor sword +10
    :
      equipL ice sword +10
      equipR vigor sword +10
  :?foe.GetCount(15) >= 3
    equipL ice wand +10
    equipR ice wand +9
  :
    equipL ice sword +10
    equipR ice shield
  

?loc = canyon & summon.count = 1
  ?foe = xyloalgia
    ?foe.state ! 32
      bardiche_use()
      ?bar_on_cd() & foe.distance <= 10
        equipL vigor sword +10
        equipR vigor sword +9
      :
        equipL vigor sword +10
        equipR vigor sword +9
    :?foe.state = 32 & foe.time = 30
      equipL mind
  :?foe = poena
    equipL aether hammer +10
    equipR aether hammer +9
  :?foe = wasp & foe.distance <= 15
    equipL ice wand +10
    equipR ice wand +9
  :?foe.distance <= 15
    equipL vigor sword +10
    equipR vigor sword +9
    ?foe.GetCount(15) >= 3
      equip skeleton arm
  :
    equipL vigor sword +10
    equipR vigor sword +9

?loc = rocky & summon.count = 1
  bardiche_use()
  ?bar_on_cd()
    ?foe = acronian
      equipL vigor sword +10
      equipR vigor sword +9
    :?foe = ice
      equipL fire sword +10
      equipR fire sword +9
    :?foe = fire
      equipL aether sword +10
      equipR aether sword +9
    :?foe = poison
      equipL ice wand +10
      equipR ice wand +9
    :?foe = vigor
      equipL poison sword +10
      equipR poison wand +10
    :
      equipL vigor sword +10
      equipR vigor sword +9

bdashstaff_use()
lootwalk()
armor_regen()
blade_use()
?harvest = Tree & harvest.distance < 10
  equip hatchet
?harvest = Boulder & harvest.distance < 10
  equip shovel
