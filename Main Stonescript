var poisonsword1 = "poison sword +11"
var poisonsword2 = "poison sword +10"
var vigorsword1 = "vigor sword +10"
var vigorsword2 = "vigor sword +9"
var aethersword1 = "aether sword +10"
var aethersword2 = "aether sword +9"
var firesword1 = "fire sword +15"
var firesword2 = "fire sword +14"
var icesword1 = "ice sword +10"
var icesword2 = "ice sword +9"
var aetherhammer1 = "aether hammer +10"
var aetherhammer2 = "aether hammer +9"
var icewand1 = "ice wand +15"
var icewand2 = "ice wand +13"
var regenshield1 = "compound shield +15"
var regenshield2 = "compound shield +14"

var sword1 = sword1_ele_check()
var sword2 = sword2_ele_check()

func sword1_ele_check()
  ?foe = poison
    return "icesword1"
  :?foe = vigor
    return "poisonsword1"
  :?foe = aether
    return "vigorsword1"
  :?foe = fire
    return "aethersword1"
  :?foe = ice
    return "firesword1"
  :
    return "vigorsword1"

func sword2_ele_check()
  ?foe = poison
    return "icesword2"
  :?foe = vigor
    return "poisonsword2"
  :?foe = aether
    return "vigorsword2"
  :?foe = fire
    return "aethersword2"
  :?foe = ice
    return "firesword2"
  :
    return "vigorsword2"

//============================================//

>c-45,-7,== LVL TIMES ==
>c-45,-6,BST: @loc.bestTime@
>c-45,-5,AVG: @loc.averageTime@
>c-45,-4,CUR: @totaltime@
>c-45,-2,== ENEMY INFO ==
>c-45,-1,Name: @foe.name@
>c-45,0,Dist: @foe.distance@
^,@foe.GetCount(15)@
>c-45,1,S/T:  @foe.state@,@foe.time@
>c-45,3,== ITEM COOLDOWNS ==
>c-45,4,BFG: @blade_cd@
>c-35,4,BAR: @item.GetCooldown(＂bardiche＂)@
>c-45,5,HAM: @item.GetCooldown(＂hammer＂)@
>c-35,5,MSK: @mask_cd@
>c-45,6,BSH: @item.GetCooldown(＂bash＂)@
>c-35,6,DSH: @item.GetCooldown(＂dash＂)@
>c-45,7,STF: @item.GetCooldown(＂quarterstaff＂)@
>c-35,7,MND: @item.GetCooldown(＂mind＂)@
?item.GetCooldown(＂blade＂) <= 0 
  >c-40,4,#44D62C,RDY
?item.GetCooldown(＂bardiche＂) <= 0 
  >c-30,4,#44D62C,RDY
?item.GetCooldown(＂hammer＂) <= 0 
  >c-40,5,#44D62C,RDY
?item.GetCooldown(＂mask＂) <= 0 
  >c-30,5,#44D62C,RDY
?item.GetCooldown(＂bash＂) <= 0 
  >c-40,6,#44D62C,RDY
?item.GetCooldown(＂dash＂) <= 0 
  >c-30,6,#44D62C,RDY
?item.GetCooldown(＂quarterstaff＂) <= 0 
  >c-40,7,#44D62C,RDY
?item.GetCooldown(＂mind＂) <= 0 
  >c-30,7,#44D62C,RDY
>c-45,10,BUFFS:   @buffs.string@
>c-45,11,DEBUFFS: @debuffs.string@

//============================================//

var loopCount = 0
?loc.loop
  loopCount++

var blade_cd = item.GetCooldown(＂blade＂)
?item.GetCooldown(＂blade＂) > 999
  blade_cd = ＂999+＂
:
  blade_cd = item.GetCooldown(＂blade＂)

var mask_cd = item.GetCooldown(＂mask＂)
?item.GetCooldown(＂mask＂) > 999
  mask_cd = ＂999+＂
:
  mask_cd = item.GetCooldown(＂mask＂)

func bar_on_cd()
  ?item.GetCooldown(＂bardiche＂) > 0
  ^& item.GetCooldown(＂bardiche＂) < 870
    return true

func ham_on_cd()
  ?item.GetCooldown(＂hammer＂) > 0
  ^& item.GetCooldown(＂hammer＂) < 645
    return true

func lootwalk()
  ?pickup.distance < 15 & summon.count = 1
    equipL triskelion
    equipR star
  ?foe.distance > 23 & armor > 24
  ^& summon.count = 1
    equipL triskelion
    equipR quest

func bardiche_use()
  ?foe.distance <= 10 & item.CanActivate()
  ^& item.GetCooldown(＂bardiche＂) <= 0 
  ^| item.GetCooldown(＂bardiche＂) > 870
    equip bardiche
    activate R

func hammer_use()
  ?foe.distance <= 7 & item.CanActivate()
  ^& item.GetCooldown(＂hammer＂) <= 0
  ^| item.GetCooldown(＂hammer＂) > 645
  ^& item.GetCooldown(＂bardiche＂) > 0
    equip heavy hammer
    activate R

func mask_use()
  ?item.GetCooldown(＂mask＂) <= 0
  ^& item.CanActivate()
  ^& foe.distance < 25
    equip mask
    activate R
 
func blade_use()
  ?foe.GetCount(30) > 7 & foe ! boss &
  ^item.GetCooldown(＂blade＂) <= 0
    equip blade
    activate R
 
func bdashstaff_use()
  ?foe.distance <= 16 & foe.distance >= 11 & 
  ^item.GetCooldown(＂bash＂) <= 0
  ^& summon.count = 1 & foe ! yeti
    equipR bashing shield
  ?foe.distance <= 16 & foe.distance >= 11 & 
  ^item.GetCooldown(＂dash＂) <= 0
  ^& summon.count = 1 & foe ! yeti
    equipR dashing shield
  ?item.GetCooldown(＂quarterstaff＂) <= 0
  ^& item.CanActivate() & foe.distance > 6
  ^& summon.count = 1 & foe ! yeti & foe ! bolesh
    equip quarterstaff
    activate R

func aether_summ()
  ?summon.count = 0 & item.CanActivate()
  ^& item.GetCooldown(＂aether_talisman＂) <= 0
    equipL triskelion
    equipR aether talisman
    activate R

func fire_summ()
  ?summon.count = 0 & item.CanActivate()
  ^& item.GetCooldown(＂fire_talisman＂) <= 0
    equipL triskelion
    equipR fire talisman
    activate R

func armor_regen()
  ?armor < 14 & foe.distance > 23
  ^& summon.count = 1
    equipL triskelion
    equipR @regenshield1@
  ?armor <= 26 & armor >= 14
  ^& foe.distance > 23
  ^& summon.count = 1
    equipL triskelion
    equipR @regenshield2@
  ?pickup.distance < 15
    equipL star
    equipR @regenshield1@

//============================================//

func isEquippedTwoHanded()
  return !item.left
func AAC()
  ?!ai.enabled
    return
  var isTwoHanded = isEquippedTwoHanded()
  var leftItem = item.left
  var rightItem = item.right 
  ?item.right.state = 3
    equipR shield *0 +0
  ?item.left.state = 3
    equipL wand *0 +0
  ?isTwoHanded
    ?rightItem
      equip @rightItem@
    :?leftItem
      equip @leftItem@
  :
    ?rightItem
      equipR @rightItem@
    ?leftItem
      equipL @leftItem@

//============================================//

AAC()

?loc = rocky & summon.count = 0
  fire_summ()
:? summon.count = 0
  aether_summ()

?loc = temple & summon.count = 1
  ?foe = nagaraja
      activate potion
      equip ice crossbow
  :?foe = acronian
    mask_use()
    bardiche_use()
    ?bar_on_cd()
      equipL @icesword1@
      equipR @icesword2@
  :?foe.GetCount(15) >= 3
    equipL @icewand1@
    equipR @icewand2@
  :?foe ! boss & armor > 40
    equipL @icesword1@
    equipR @icesword2@
  :
    equipL @icesword1@
    equipR ice shield

?loc = ridge & summon.count = 1
  ?foe = yeti
    ?foe.armor > 0
      bardiche_use()
    ?foe.state = 132
      activate potion
    :?bar_on_cd()
      equip fire crossbow
  :?foe.GetCount(15) >= 3
    equipL @icewand1@
    equipR @icewand2@
  :?foe = ice pillar
    equipL @firesword1@
    equipR @firesword2@
  :?foe = ice wall
    equip fire crossbow
  :?foe = giant
    ?foe.state = 2 & foe.time = 50
      equip mask
      activate R
    ?hp < maxhp
      equipL @vigorsword1@
      equipR @vigorsword2@
    :
      equipL @firesword1@
      equipR @firesword2@
  :
    equipL @firesword1@
    equipR fire shield

?loc = mine & summon.count = 1
  ?foe = guardian
    ?foe.state = 2
      activate potion
      equip aether crossbow
    :?foe.state = 33
      ?foe.distance <= 7
        bardiche_use()
      ?foe.armor > 1 & foe.distance <= 6
      ^& bar_on_cd()
        hammer_use()
      ?foe.distance < 19 & foe.distance > 10
      ^& foe.armor > 1
        equipL @aetherhammer1@
        equipR @aetherhammer2@
      ?foe.distance < 10 & foe.armor > 1
      ^& bar_on_cd() & ham_on_cd()
        equipL @aetherhammer1@
        equipR @aetherhammer2@
      ?foe.distance < 10 & foe.armor < 1
      ^& bar_on_cd() & ham_on_cd()
        equipL @aethersword1@
        equipR @aethersword2@
      ?foe.time = 210
        equip aether crossbow
    :?foe.state = 32 & foe.time = 25
      equipL mind
    :
      equip aether crossbow
  :?foe.state = 32 & foe.time = 200
    equip aether crossbow
  :?foe.distance < 17
  ^& foe.GetCount(15) >= 3
    equipL @icewand1@
    equipR @icewand2@
  :?foe = bomb & foe.distance < 5
    equipL mind
  :?foe ! boss & armor > 40
    equipL @aethersword1@
    equipR @aethersword2@
  :
    equipL @aethersword1@
    equipR aether shield

?loc = halls & summon.count = 1
  ?foe.GetCount(15) >= 3 | foe = ghost
  ^& foe.distance <= 15
    equipL @icewand1@
    equipR @icewand2@
  :?foe = pallas
    ?foe.state = 2 & foe = legless
      activate potion
    ?foe.distance <= 7
      bardiche_use()
    ?bar_on_cd()
      equipL @vigorsword1@
      equipR @vigorsword2@
    ?foe.GetCount(15) >= 3 | foe = ghost
    ^& foe.distance <= 15
      equipL @icewand1@
      equipR @icewand2@
  :
    equipL @vigorsword1@
    equipR @vigorsword2@

?loc = forest & summon.count = 1
  ?foe.distance <= 15
    ?foe = puff & foe.distance < 5
      equipL mind
    :?foe = boss
      bardiche_use()
      ?bar_on_cd() & foe.distance <= 10
        equipL @vigorsword1@
        equipR @vigorsword2@
    :?foe.GetCount(15) >= 3
      equipL @icewand1@
      equipR @icewand2@
    :
      equipL @vigorsword1@
      equipR @vigorsword2@

?loc = caves & summon.count = 1
  ?foe = bolesh
    ?foe.state = 1
      activate potion
      equip ice crossbow
  :?foe = ceiling
    bardiche_use()
    ?foe.state = 2 & bar_on_cd()
    ^& item.GetCooldown(＂mind＂) <= 0
      equipL mind
      equipR @icesword1@
    :?bar_on_cd() & foe.distance < 10
      equipL @icesword1@
      equipR @icesword2@
  :?foe.GetCount(15) >= 3
    equipL @icewand1@
    equipR @icewand2@
  :
    equipL @icesword1@
    equipR ice shield
  

?loc = canyon & summon.count = 1
  ?foe = xyloalgia
    ?foe.state ! 32
      bardiche_use()
      ?bar_on_cd() & foe.distance <= 10
        equipL @vigorsword1@
        equipR @vigorsword2@
      :
        equipL @vigorsword1@
        equipR @vigorsword2@
    :?foe.state = 32 & foe.time = 30
      equipL mind
  :?foe = poena
    equipL @aetherhammer1@
    equipR @aetherhammer2@
  :?foe = wasp & foe.distance <= 15
    equipL @icewand1@
    equipR @icewand2@
  :?foe.distance <= 15
    equipL @vigorsword1@
    equipR @vigorsword2@
    ?foe.GetCount(15) >= 3
      equip skeleton arm
  :
    equipL @vigorsword1@
    equipR @vigorsword2@

?loc = rocky & summon.count = 1
  bardiche_use()
  ?bar_on_cd()
    ?foe = acronian
      equipL @vigorsword1@
      equipR @vigorsword2@
    :?foe = ice
      equipL @firesword1@
      equipR @firesword2@
    :?foe = fire
      equipL @aethersword1@
      equipR @aethersword2@
    :?foe = poison
      equipL @icewand1@
      equipR @icewand2@
    :?foe = vigor
      equipL @poisonsword1@
      equipR @poisonsword2@
    :
      equipL @vigorsword1@
      equipR @vigorsword2@

bdashstaff_use()
lootwalk()
armor_regen()
blade_use()
//?harvest = Tree & harvest.distance < 10
//  equip hatchet
//?harvest = Boulder & harvest.distance < 10
//  equip shovel