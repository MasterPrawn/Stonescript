>c-45,-7,== LVL TIMES ==
>c-45,-6,BST: @loc.bestTime@
>c-45,-5,AVG: @loc.averageTime@
>c-45,-4,CUR: @totaltime@
>c-45,-2,== ENEMY INFO ==
>c-45,-1,Name: @foe.name@
>c-45,0,Dist: @foe.distance@
^,@foe.GetCount(15)@
>c-45,1,S/T:  @foe.state@,@foe.time@
>c-45,3,== CD STATUS ==
>c-45,4,BFG: @item.GetCooldown("blade")@
^,BAR: @item.GetCooldown("bardiche")@
>c-45,5,HAM: @item.GetCooldown("hammer")@
^,STF: @item.GetCooldown("quarterstaff")@
>c-45,6,BSH: @item.GetCooldown("bash")@
^,DSH: @item.GetCooldown("dash")@

>c-45,10,@buffs.count@
>c-45,11,@buffs.string@

// "dysangelos,xyloalgia,poena,bolesh,
//  ^shroom,morel,enoki,pallas,guardian,hrimnir,
//  ^nagaraja"
//==========================================//

func lootwalk()
  ?pickup.distance < 15 & summon.count = 1
    equipL triskelion
    equipR star
  ?foe.distance > 23 & armor > 24
  ^& summon.count = 1
    equipL triskelion
    equipR ouroboros

func bardiche_use()
  ?foe.distance <= 5 & item.CanActivate()
  ^& item.GetCooldown("bardiche") <= 0
  ^| item.GetCooldown("bardiche") > 870
    equip bardiche
    activate R

func hammer_use()
  ?foe.distance <= 7 & item.CanActivate()
  ^& item.GetCooldown("hammer") <= 0
  ^| item.GetCooldown("hammer") > 630
  ^& item.GetCooldown("bardiche") > 0
    equip heavy hammer
    activate R

func mask_use()
  ?item.GetCooldown("mask") <= 0
  ^& item.CanActivate()
  ^& foe.distance < 25
    equip mask
    activate R
 
func blade_use()
  ?foe.GetCount(40) > 5 & foe ! boss &
  ^item.GetCooldown("blade") <= 0
    equip blade
    activate R
 
func bdashstaff_use()
  ?foe.distance <= 16 & foe.distance >= 11 & 
  ^item.GetCooldown("bash") <= 0
  ^& summon.count = 1
    equipR bashing shield
  ?foe.distance <= 16 & foe.distance >= 11 & 
  ^item.GetCooldown("dash") <= 0
  ^& summon.count = 1
    equipR dashing shield
  ?item.GetCooldown("quarterstaff") <= 0
  ^& item.CanActivate() & foe.distance > 6
  ^& summon.count = 1
    equip quarterstaff
    activate R

func aether_summ()
  ?summon.count = 0 & item.CanActivate()
  ^& item.GetCooldown("aether_talisman") <= 0
    equipL triskelion
    equipR aether talisman
    activate R

func fire_summ()
  ?foe = boss & summon.GetId() = "voidweaver"
  ^& item.CanActivate()
  ^& item.GetCooldown("fire_talisman") <= 0
    equipL fire talisman
    activate L

func armor_regen()
  ?armor < 14 & foe.distance > 23
  ^& summon.count = 1
    equipL triskelion 
    equipR compound shield +11
  ?armor <= 24 & armor >= 14
  ^& foe.distance > 23 & summon.count = 1
    equipL triskelion
    equipR compound shield +10
  ?pickup.distance < 15 & summon.count = 1
    equipL star
    equipR compound shield +11

//==========================================//

func isEquippedTwoHanded()
  return !item.left
func AAC()
  ?!ai.enabled
    return
  var isTwoHanded = isEquippedTwoHanded()
  var leftItem = item.left
  var rightItem = item.right 
  ?item.right.state = 3
    equipR shield *0 +0
  ?item.left.state = 3
    equipL wand *0 +0
  ?isTwoHanded
    ?rightItem
      equip @rightItem@
    :?leftItem
      equip @leftItem@
  :
    ?rightItem
      equipR @rightItem@
    ?leftItem
      equipL @leftItem@

//==========================================//

?loc.begin | loc.loop
  ?loc = halls
    brew wood + bronze

AAC()

?loc = rocky & summon.count = 1
  lootwalk()
  ?foe = ice
    equipL fire sword +10
    equipR fire sword +9
  :?foe = fire
    equipL aether sword +10
    equipR aether sword +9
  :
    equipL vigor sword +10
    equipR vigor sword +9

?loc = halls & summon.count = 1
  brew wood + bronze
  ?foe.distance < 17
    ?foe.GetCount(15) >= 3 | foe = ghost
    ^& foe.distance <= 15
      equipL ice wand +10
      equipR ice wand +9
    :?foe = pallas
      ?foe.state = 2 & foe = legless
        activate potion
      ?foe.distance <= 7
        bardiche_use()
        ?item.GetCooldown("bardiche") > 0
        ^& item.GetCooldown("bardiche") < 870
        ^& foe.distance <= 7
          equipL vigor sword +10
          equipR vigor sword +9
      ?foe.GetCount(15) >= 3 | foe = ghost
      ^& foe.distance <= 15
        equipL ice wand +10
        equipR ice wand +9
    :
      equipL vigor sword +10
      equipR vigor sword +9

?loc = forest & summon.count = 1
  ?foe.distance <= 15
    ?foe = puff & foe.distance < 5
      equipL mind
    :?foe = boss
      bardiche_use()
      ?item.GetCooldown("bardiche") > 0
      ^& item.GetCooldown("bardiche") < 870
      ^& foe.distance <= 10
        equipL vigor sword +10
        equipR poison sword +10
    :?foe.GetCount(15) >= 3
      equipL ice wand +10
      equipR ice wand +9
    :
      equipL vigor sword +10
      equipR poison sword +10

?loc = mine & summon.count = 1
  ?foe.GetCount(15) >= 3
    equipL ice wand +10
    equipR aether shield
  :?foe = bomb & foe.distance < 5
    equipL mind
  :?foe = guardian
    ?foe.state = 2
      activate potion
    ?foe.state = 33
      ?foe.distance < 25 & foe.distance > 10
      ^& foe.armor > 1
        equipL aether sword +10
        equipR aether hammer +10
      ?foe.armor > 1 & foe.distance <= 6
        hammer_use()
        ?item.GetCooldown("hammer") > 0
        ^& item.GetCooldown("hammer") < 630
        ^& foe.distance <= 6
        ^& foe.armor > 1
          equipL aether sword +10
          equipR aether hammer +10
      ?foe.distance <= 7
        bardiche_use()
        ?item.GetCooldown("bardiche") > 0
        ^& item.GetCooldown("bardiche") < 870
        ^& foe.distance <= 7
        ^& foe.armor > 1
          equipL aether sword +10
          equipR aether hammer +10
    :?foe.state = 32 & foe.time = 25
      equipL mind
    :?foe.state = 32 & foe.time = 200
      equip aether crossbow
    :
      equip aether crossbow
  :
    equipL aether sword +10
    equipR aether shield

?loc = ridge & summon.count = 1
  ?foe.distance <= 15
    ?foe = boss
      ?foe.armor > 0
        equipL fire sword +10
        equipR fire sword +9
      :?foe.distance = 3
        equip shovel
      :?foe.distance < 3
        bardiche_use()
        ?item.GetCooldown("bardiche") > 0
        ^& item.GetCooldown("bardiche") < 870
        ^& foe.distance <= 10
          equipL fire sword +10
          equipR fire sword +9
    :?foe.GetCount(15) >= 3
      equipL ice wand
      equipR fire shield
    :?foe = ice wall | foe = ice pillar
      equipL fire sword +10
      equipR fire sword +9
    :
      equipL fire sword +10
      equipR fire shield

aether_summ()
bdashstaff_use()
lootwalk()
armor_regen()
blade_use()
